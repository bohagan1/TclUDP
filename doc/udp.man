[comment {-*- tcl -*- doctools manpage}]
[comment {To convert this to another documentation format use the mpexpand
          script from tcllib: dtplite -o udp.n nroff udp.man
                              dtplite -o udp.html html udp.man
}]
[manpage_begin udp n 1.0.12]
[copyright {1999-2000 Columbia University; all rights reserved}]
[moddesc {Tcl UDP extension}]
[titledesc {Create UDP sockets in Tcl}]
[require Tcl 8.5]
[require udp 1.0.12]
[description]

This package provides support for using UDP through Tcl. The package provides
a new channel type and attempts to permit the use of packet oriented UDP
over stream oriented Tcl channels. The package defined three commands but
[cmd udp_conf] should be considered depreciated in favor of the standard
Tcl command [cmd fconfigure].

[section "COMMANDS"]

[list_begin definitions]

[call [cmd "udp_open"] [opt "[arg localport]"] [opt "[option reuse]"] [opt "[option ipv6]"]]

[cmd udp_open] will open a UDP socket. If a [arg localport] is specified the UDP
socket will be opened on that port. Otherwise the system will choose a port
and the user can use the [cmd udp_conf] command to obtain the port number
if required.

[para]
The following keywords can be used to specify options on the opened socket.

[list_begin definitions]

[def "[option reuse]"]
Using this keyword sets the SO_REUSEADDR socket option which permits multiple
sockets to be bound to the same address/port combination.

[def "[option ipv6]"]
By default a IPv4 socket is created. When keyword [option ipv6] is specified, an IPv6
socket is opened.

[list_end]

[call [cmd "udp_conf"] [arg "channel"] [arg "host"] [arg "port"]]

[emph Deprecated] in favor of the standard Tcl [cmd fconfigure] or
[cmd "chan configure"] commands.
[para]
[cmd udp_conf] in this configuration is used to specify the remote destination
for packets written to this [arg "channel"]. You must call this command before
writing data to the UDP socket.

[call [cmd "udp_conf"] [arg "channel"] [arg [opt "optionName"]] [arg [opt "value"]] [arg [opt "optionName value ..."]]]

[emph Deprecated] in favor of the standard Tcl [cmd fconfigure] or
[cmd "chan configure"] commands.
[para]
In addition to being used to configure the remote host, the [cmd "udp_conf"]
command is used to obtain information about the UDP socket. NOTE all these
options are now available using the standard Tcl [cmd fconfigure] or
[cmd "chan configure"] command.

[list_begin definitions]

[def "[option -myport]"]
Returns the local port number of the socket. Read-only option.

[def "[option -remote] [arg [opt "address port"]]"]
Specifies or returns the remote hostname and port number. Can also be set using
[cmd udp_conf] [arg channel] [arg host] [arg port].

[def "[option -peer]"]
Returns the remote hostname and port number for the packet most recently
received by this socket. Read-only option.

[def "[option -family]"]
Returns whether socket is configured for IPv4 or IPv6. Read-only option.

[def "[option -broadcast] [arg [opt boolean]]"]
Specifies or returns whether can listen and send on the broadcast address. For some systems
a flag must be set on the socket to use broadcast. This option is not permitted when
using IPv6, instead use multicast.

[def "[option -ttl] [arg [opt count]]"]

The time-to-live is given as the number of router hops the packet may do. For
multicast packets this is important in specifying the distribution of the
packet. The system default for multicast is 1 which restricts the packet
to the local subnet. To permit packets to pass routers, you must increase the
ttl. A value of 31 should keep it within a site, while 255 is global.

[def "[option -mcastadd] groupaddr"]
[def "[option -mcastadd] \"groupaddr netwif\""]
[def "[option -mcastdrop] groupaddr"]
[def "[option -mcastdrop] \"groupaddr netwif\""]
[def "[option -mcastgroups]"]

[package tcludp] sockets can support IPv4 and IPv6 multicast operations. To receive
multicast packets the application has to notify the operating system that
it should join a particular multicast group. For IPv4 these are specified as addresses
in the range 224.0.0.0 to 239.255.255.255.
[para]
When specifying only the [arg groupaddr] the system will determine the network interface to use.
Specifying the [arg netwif] will join a multicast group on a specific network interface.
This is useful on a multihomed system with multiple network interfaces.
On windows you must specify the network interface index. For other platforms the network
interface (e.g. 'eth0') name can be specified.
[para]
To view the current set of multicast groups for a channel use [arg -mcastgroups]

[def "[option -mcastif]"]

Returns which interface is used for outgoing multicast packets. UNIX only.

[def "[option -mcastloop] [arg [opt boolean]]"]

With multicast udp the system can choose to receive packets that it has sent
or it can drop them. This is known as multicast loopback and can be controlled
using this option. By default the value is true and your application will receive
its own transmissions.

[list_end]

[call [cmd "udp_peek"] [arg "channel"] [opt [arg "buffersize"]]]

Examine a packet without removing it from the buffer. Option [arg buffersize] specifies the
maximum buffer size. Value must be between 0 and 16.
[para]
This function is not available on windows.

[call [cmd "::udp::build-info"]]

Return information on the build environment.

[call [cmd "::udp::getaddrinfo"] [arg "[option -hostname] name"] [arg [opt "optionName value ..."]]]

Returns a list with info on the IP address matching the specified parameters.
Valid options are:

[list_begin definitions]

[def "[option -hostname] [arg name]"]
Specifies which IP address to use for lookup. Can use IP address or name. Required option.

[def "[option -port] [arg number]"]
Specifies which port number to use for lookup.

[def "[option -service] [arg name]"]
Specifies which well-known service (i.e. http, https, etc.) to use for port in lookup.

[def "[option -ipv4]"]
Only return IP v4 addresses.

[def "[option -ipv6]"]
Only return IP v6 addresses.

[def "[option -server]"]
Only return services for which hostname is a server.

[def "[option -tcp]"]
Only return TCP services.

[def "[option -udp]"]
Only return UDP services.

[list_end]

[call [cmd "::udp::getnameinfo"] [arg "address"] [option "[opt ipv6]"]]

[cmd ::udp::getnameinfo] will return the name(s) corresponding to IP
address [arg address]. With [arg ipv6], address is an IPv6 address.

[list_end]

[section EXAMPLES]
[para]
[example {
# Send data to a remote UDP socket
proc udp_puts {host port} {
    set s [udp_open]
    fconfigure $s -remote [list $host $port]
    puts $s "Hello, World"
    close $f
}
}]

[example {
# A simple UDP server
package require udp

proc udpEventHandler {sock} {
    set pkt [read $sock]
    set peer [fconfigure $sock -peer]
    puts "$peer: [string length $pkt] {$pkt}"
    return
}

proc udp_listen {port} {
    set srv [udp_open $port]
    fconfigure $srv -buffering none -translation binary
    fileevent $srv readable [list ::udpEventHandler $srv]
    puts "Listening on udp port: [fconfigure $srv -myport]"
    return $srv
}

set sock [udp_listen 53530]
vwait forever
close $sock
}]

[example {
# A multicast demo.
proc udpEvent {chan} {
    set data [read $chan]
    set peer [fconfigure $chan -peer]
    puts "$peer [string length $data] '$data'"
    if {[string match "QUIT*" $data]} {
        close $chan
        set ::forever 1
    }
    return
}

set group 224.5.1.21
set port  7771
set s [udp_open $port]
fconfigure $s -buffering none -blocking 0
fconfigure $s -mcastadd $group -remote [list $group $port]
fileevent $s readable [list udpEvent $s]
puts -nonewline $s "hello, world"
set ::forever 0
vwait ::forever
exit
}]

[section "HISTORY"]

Some of the code in this extension is copied from Michael Miller's tcludp
package. (http://www.neosoft.com/tcl/ftparchive/sorted/comm/tcludp-1.0/)
Compared with Michael's UDP extension, this extension provides Windows
support and provides the ability of using 'gets/puts' to read/write
the socket. In addition, it provides more configuration ability.

[para]

Enhancements to support binary data and to setup the package for the Tcl
Extension Architecture by Pat Thoyts.

[para]
Support for IPv6 and allowing a multicast join on a specific network interface is added by Huub Eikens.

[see_also socket(n)]
[keywords udp socket networking]
[manpage_end]
